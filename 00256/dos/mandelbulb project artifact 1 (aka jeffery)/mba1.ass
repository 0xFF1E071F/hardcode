;
;	mandlealub 256byte demo by alih/wow/unreal
;
	org 100h
rot	equ	-28
wx	equ	-24
wy	equ	-20
wz	equ	-16
ox	equ	-12
oy	equ	-8
oz	equ	-4


; constants
maxiter	equ	8

; take the 16 freom the int 10h
sixteen equ 103h

	; set mode 13h
	mov al,13h
	int 10h

	; set es: to screen
	push 0a000h		; also used for rlimit - remmeber to change that if we change this!!!
	pop es

	; screen pointer
	mov bx,eight
loopi:
	; ax is set to 90 at the bottom of this loop - half way between 80 (width) and 100 (height) :)
	; so that it is preserved across both x and y loops
	inc word [bp+rot]
;	mov di,64000
	mov word [bp+wy],ax
	;fninit
	; yloop
	; set st0 to -1 for y
	;fld1
	;fstp dword [bp+wy]
ypxloop:
	mov dl,160
	mov word [bp+wx],ax
	; set st1 to -1 for x
	;fld1
	;fstp dword [bp+wx]
xpxloop:

	; set st2 to -1 for z
	mov al,15
	mov word [bp+wz],ax
zloop:
	fninit

	fld dword[zstep]
	fimul word[bp+rot]
	fsincos

eight 	equ	$+1
	mov si,8
cxl:
	fild word [bp+wx+si]
	fmul dword[xstep+si]
	sub si,4
	jnc cxl
				
	; x -> cos()x + sin()cos()y + sin()sin()z
	; y -> -sin()x+cos()cos()y+cos()sin()z
	; z -> -sin()y+cos()z
	; state: so0->x,st1->y,st2->z,st3->cos,st4->sin



	; z -> -sin()y+cos()z

	; state: so0->x,st1->y,st2->z,st3->cos,st4->sin
	fld st1
	; state: sto->z',st1->x,st2->y,st3->z,st4->cos,st5->sin
	fmul st5
	fld st3
	; state: st1->zy, st1->z',st2->x,st3->y,st4->z,st5->cos,st6->sin
	fmul st5
	fsubp st1
	fst dword [bp+oz]
	; state: sto->z',st1->x,st2->y,st3->z,st4->cos,st5->sin

	; x -> cos()x + sin()cos()y + sin()sin()z
	; state: sto->z',st1->x,st2->y,st3->z,st4->cos,st5->sin
	fld st1
	; state: sto->x',st1->z',st2->x,st3->y,st4->z,st5->cos,st6->sin
	fmul st5
	fld st3
	; state: sto->x',st1->t,st2->z',st3->x,st4->y,st5->z,st6->cos,st7->sin
	fmul st6
	fmul st7
	faddp st1
	; state: sto->x',st1->z',st2->x,st3->y,st4->z,st5->cos,st6->sin
	fld st4
	; state: sto->x',st1->t,st2->z',st3->x,st4->y,st5->z,st6->cos,st7->sin
	fmul st7
	fmul st7
	faddp st1
	; state: sto->x',st1->z',st2->x,st3->y,st4->z,st5->cos,st6->sin
	fst dword [bp+ox]

	; y -> -sin()x+cos()cos()y+cos()sin()z
	; state: sto->x',st1->z',st2->x,st3->y,st4->z,st5->cos,st6->sin
	fld st3
	; state: sto->y',st1->x',st2->z',st3->x,st4->y,st5->z,st6->cos,st7->sin
	fmul st6
	fmul st6
	fxch st0,st6
	; state: sto->cos,st1->x',st2->z',st3->x,st4->y,st5->z,st6->y',st7->sin
	fmul st7
	fmul st5
	faddp st6,st0
	; state: sto->x',st1->z',st2->x,st3->y,st4->z,st5->y',st6->sin
	fld st6
	; state: sto->t,st1->x',st2->z',st3->x,st4->y,st5->z,st6->y',st7->sin
	fmul st3
	fsubr st6
	; state: sto->y',st1->x',st2->z',st3->x,st4->y,st5->z,st6->y',st7->sin
	fst dword [bp+oy]


	; state: st0->x',st0->y', st1->z'
	; set maxIter
	mov dh,maxiter

iterloop:
	; okay, go!
	ffree st3
	ffree st4
	ffree st5
	ffree st6
	ffree st7
	; state: x,y,z = st0,st1,st2

	; each time we do this for x,y,z, it pushes to the stack
	; x=st0,y=st1,z=st2
	; square it
	mov cx,3
cccd:
	fld st2
	fmul st0	; z^2,x^2,y^2
	; z2=st0,x=st1,y=st2,z=st3
	; y2=st0,z2=st1,x=st2,y=st3,z=st4
	; x2=st0,y2=st1,z2=st2,x=st3,y=st4,z=st5
	loop cccd

	; add the two together for the beta calc
	faddp st1

	; state x2y2=st0,z2=st1,x=st2,y=st3,z=st4
	; add z^2 to the whole thing to make r :)
	fadd st1,st0
	; state x2y2=st0,r=st1,x=st2,y=st3,z=st4

	; check for hugeness of r, since that'll cause
	; FP overflow
	fld dword [rlimit]
	fcomip st2
	jb overflow		; the order of this is dumb!!

	; check if we're out by iterating!
	dec dh
	jz loopOver

	; i guess not, lets keep calculating!

	; atan2(sqrt(x2y2), z)
	; note that fpatan takes x,y rather than the y,x of atan2!!
	; the good news is that the algorythm doesn't actually really care, and we can just
	; reverse it for the convincience of x87 is shit :)
	fsqrt
	fld st4
	fpatan

	; multiply that by 8
	fimul word [bx]

	; woo, beta calculated!
	; state beta=st0,r=st1,x=st2,y=st3,z=st4
	; put cos/sin of beta on the stack now, we need them later!
	fsincos
	; state cos(beta)=st0,sin(beta)=st1,r=st2,x=st3,y=st4,z=st5

	; shit, we need r8, but we're out of regs.  shiiit. gotta waste two bytes moving r to the top....
	; win tho - sqrt(r)^8, which is r^4, which is (r^2)^2 - thanks, newton!
	fxch st0,st2
	fmul st0
	fmul st0
	fxch st0,st2

	; we can use cos beta now, and remove it :)
	; z=r8*cos(beta) + bp+oz
	fmul st0,st2
	fadd dword [bp+oz]
	; state z'=st0,sin(beta)=st1,r=st2,x=st3,y=st4,z=st5


	; alpha tgime - this is easier :)
	; atan2(y,x)*8 - st4,st3 :)
	fld st3
	fld st5
	fpatan
	fimul word[bx]
	fsincos
	; state cos(alpha)=st0,sin(alpha)=st1,z'=st2,sin(beta)=st3,r8=st4,x=st5,y=st6,z=st7

	; x = rp * sin(beta) * cos(alpha) + bp+oz;
	fmul st0,st4
	fmul st0,st3
	fadd dword [bp+ox]
	; state x'=st0,sin(alpha)=st1,z'=st2,sin(beta)=st3,r8=st4,x=st5,y=st6,z=st7

	; y = rp * sin(beta) * sin(alpha) + bp+oy;
	fxch st0,st1
	; state sin(alpha),x'=st1,z'=st2,sin(beta)=st3,r8=st4,x=st5,y=st6,z=st7
	fmul st0,st4
	fmul st0,st3
	fadd dword[bp+oy]
	; state y'=st0,x'=st1,z'=st2,sin(beta)=st3,r8=st4,x=st5,y=st6,z=st7
	;fxch st0,st1
	; state x'=st0,y'=st1,z'=st2,sin(beta)=st3,r8=st4,x=st5,y=st6,z=st7

	; state x=st0,y=st0,z=st2

	; iterate!!! the escape clause is just afetr the calc of r...
	jmp short iterloop
loopOver:
	; okay, we fell out of the loop of natural
	; causes - which means we this one is a hit!
	; maybe....
	; state here: temp=st0,r=st1,x=st2;y=st3,z=st4
	
	; state here: r=st0,x=st1;y=st2,z=st3
	; dist=sqrt(r) for now!
	; but we can keep it not sqrt'd forever because
	; we don';t have enough bytes for normals anyhow :)
	;fsqrt
	fild word [bx]
	fcomip st1
	jnb foundZ

	; once you hit here, it MUST be 
	; state temp=st0,x=st1,y=st2,z=st3
overflow:
	; sad tiems - we didn't find shit, either due to chaos overflow or fail at dist, try the next z ;)
	dec word [bp+wz]

	; one byte less than the FPU compare - and we can reuse it for crappy colour
	dec al
	jnz zloop
	;fld1
	;fcomip st1
	;jnb zloop

	;jmp nothingFound
foundZ:
	; FIXME: get a colour???
nothingFound:
	; ghetto colour!
	; grey
	or al,16
	;mov al,ah
	mov word [es:di],ax
	
	; also 2*inc is faster than using scaled addressing mode, as far asi can tell,
	; because those opcodes are le long
	inc di		; two inc's is one byte less than an add
	inc di		; two inc's is one byte less than an add
	; set the width here, of all places ;)
	mov al,90
	je loopi
 
	; add xstep 
	dec word [bp+wx]

	dec dl
	jnz xpxloop

	
	; add ystep 
	dec word [bp+wy]

	jmp ypxloop




;keyout:
;	in al,60h
;	dec al
;	jz loopi
;	ret

; constants
;rlimit	dd	1e25
rlimit equ 101h



; 2.0/320
xstep dd -0.012500
;xstep dd 0.00625


; 2.0/200
;ystep dd 0.020000
ystep dd -0.010000

; 2.0/32...?
zstep dd -0.062500




